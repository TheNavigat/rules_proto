load("//bazel/go:defaults.bzl", "go_library")

genrule(
    name = "antlr_python_parser",
    srcs = ["Python3.g4"],
    outs = [
        "python3_base_listener.go",
        "python3_parser.go",
        "python3_listener.go",
        "python3_lexer.go",
    ],
    cmd = """
        $(JAVA) -jar "$(execpath @org_antlr//jar)" \
            -Dlanguage=Go \
            -package parser \
            "$(execpath Python3.g4)" \
            && mv "$$(dirname "$(execpath Python3.g4)")"/*.go "$(@D)"
    """,
    toolchains = ["@bazel_tools//tools/jdk:current_java_runtime"],
    tools = ["@org_antlr//jar"],
    visibility = ["//visibility:public"],
)

go_library(
    name = "parser",
    srcs = [
        "python3_base_listener.go",
        "python3_lexer.go",
        "python3_listener.go",
        "python3_parser.go",
    ],
    importpath = "github.com/stackb/rules_proto/tools/laze/parser",
    visibility = ["//visibility:public"],
    deps = [
        "@com_github_antlr_antlr4//runtime/Go/antlr:go_default_library",  # keep
    ],
)
