load(
    "@build_stack_rules_proto//rules/proto:proto_language_info_provider_test.bzl",
    "proto_language_info_provider_test",
)
load(
    "@build_stack_rules_proto//rules/proto:proto_plugin_info_provider_test.bzl",
    "proto_plugin_info_provider_test",
)
load(
    "@build_stack_rules_proto//rules/proto:proto_language_test.bzl",
    "proto_language_test",
)
load(
    "@build_stack_rules_proto//rules/proto:proto_language.bzl",
    "proto_language",
)
load(
    "@build_stack_rules_proto//rules/proto:proto_rule_info_provider_test.bzl",
    "proto_rule_info_provider_test",
)
load(
    "@build_stack_rules_proto//rules/proto:proto_rule_test.bzl",
    "proto_rule_test",
)
load(
    "@build_stack_rules_proto//rules/proto:proto_rule_bazel_test.bzl",
    "proto_rule_bazel_test",
)
load(
    "@build_stack_rules_proto//rules/proto:proto_rule.bzl",
    "proto_rule",
)
load("//plugins/gogo:variants.bzl","GOGO_VARIANTS")


proto_language(
    name = "gogo",
    avatar_url = "https://github.com/gogo.png",
    display_name = "Gogo",
    package = "rules",
    prefix = "gogo",
    rules = [variant + "_proto_compile" for variant in GOGO_VARIANTS],
    visibility = ["//visibility:public"],
)

proto_language_info_provider_test(
    name = "gogo_info_provider_test",
    srcs = ["gogo_info_provider_test.golden.1.prototext"],
    deps = [":gogo"],
)

proto_language_test(
    name = "gogo_language_test",
    srcs = ["//rules:gogo.bzl"],
    package = "rules",
    deps = [":gogo"],
)

[proto_plugin_info_provider_test(
    name = variant + "_plugin_info_provider_test",
    srcs = [variant + "_plugin_info_provider_test.golden.1.prototext"],
    deps = ["//plugins/gogo:" + variant],
) for variant in GOGO_VARIANTS]

[proto_rule(
    name = variant + "_proto_compile",
    kind = "proto",
    language = "//rules/proto/gogo",
    package = "rules",
    plugins = ["//plugins/gogo:"+variant],
    visibility = ["//visibility:public"],
) for variant in GOGO_VARIANTS]

[proto_rule_info_provider_test(
    name = variant + "_proto_compile_info_provider_test",
    srcs = [variant + "_proto_compile_info_provider_test.golden.1.prototext"],
    deps = [variant + "_proto_compile"],
) for variant in GOGO_VARIANTS]

[proto_rule_test(
    name = variant + "_proto_compile_aspect_rule_test",
    srcs = [
        "//rules:%s_proto_compile.bzl" % variant,
        "//rules:%s_proto_compile_deps.bzl" % variant,
    ],
    package = "rules",
    deps = [variant + "_proto_compile"],
) for variant in GOGO_VARIANTS]

[proto_rule_bazel_test(
    name = variant + "_proto_compile_aspect_rule_bazel_test",
    tags = ["exclusive"],
    deps = [variant + "_proto_compile"],
) for variant in GOGO_VARIANTS]
